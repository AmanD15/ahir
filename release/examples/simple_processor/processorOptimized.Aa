// Aa code produced by llvm2aa (version 1.0)
$pipe out_port : $uint<16> $depth 1

$storage mem:$array [64] $of $uint<16>

$pipe reg_file_read_port_0_request : $uint<16> $depth 4
$pipe reg_file_read_port_1_request : $uint<16> $depth 4
$pipe reg_file_read_port_2_request : $uint<16> $depth 4

$pipe reg_file_read_port_0_request_pc : $uint<16> $depth 4
$pipe reg_file_read_port_1_request_pc : $uint<16> $depth 4
$pipe reg_file_read_port_2_request_pc : $uint<16> $depth 4

$pipe reg_file_read_port_0_response : $uint<16> $depth 4
$pipe reg_file_read_port_1_response : $uint<16> $depth 4
$pipe reg_file_read_port_2_response : $uint<16> $depth 4

$pipe reg_file_write_port_request     : $uint<16> $depth 4
$pipe reg_file_write_port_request_pc  : $uint<16> $depth 4
$pipe reg_file_write_port_data : $uint<16> $depth 4

$pipe block_read_request_0: $uint<16> $depth 4
$pipe block_read_request_1: $uint<16> $depth 4
$pipe block_read_request_2: $uint<16> $depth 4

$pipe bypass_to_0 : $uint<16> $depth 4
$pipe bypass_to_1 : $uint<16> $depth 4
$pipe bypass_to_2 : $uint<16> $depth 4

$pipe bypass_pc_to_0 : $uint<16> $depth 4
$pipe bypass_pc_to_1 : $uint<16> $depth 4
$pipe bypass_pc_to_2 : $uint<16> $depth 4


$pipe start_ifetch: $uint<8>
$pipe halt_pipe : $uint<8>
$pipe ir_pipe: $uint<16>  $depth 4
$pipe next_pc_pipe : $uint<16>
$pipe pc_pipe : $uint<16> $depth 4

$module [ifetch] $in () $out () $is
{
	next_pc_pipe := 0

	sif := start_ifetch
	
	$branchblock[core] {
    		$dopipeline $depth 8 $buffering 2
			$merge $entry $loopback $endmerge
			pc_0 := next_pc_pipe
			pc_1 := (pc_0 + 1)
			pc_2 := (pc_0 + 2)
			pc_3 := (pc_0 + 3)

			// this should load from ICACHE.
			ir_0 := mem[pc_0]
			ir_1 := mem[pc_1]
			ir_2 := mem[pc_2]
			ir_3 := mem[pc_3]
	
			op_0 := (ir_0 >> 12)
			op_1 := (ir_1 >> 12)
			op_2 := (ir_2 >> 12)
			op_3 := (ir_3 >> 12)

			br_0 := ((op_0 == 0) | ((op_0 == 8) | ((op_0 == 9) | (op_0 == 10))))
			br_1 := ((op_1 == 0) | ((op_1 == 8) | ((op_1 == 9) | (op_1 == 10))))
			br_2 := ((op_2 == 0) | ((op_2 == 8) | ((op_2 == 9) | (op_2 == 10))))
			br_3 := ((op_3 == 0) | ((op_3 == 8) | ((op_3 == 9) | (op_3 == 10))))

			d_1  := (~ br_0)
			d_2  := (~ (br_0 | br_1))
			d_3  := (~ ((br_0 | br_1) | br_2) )

			branch_case := ((br_0 | br_1) | (br_2 | br_3))

			ir_pipe := ir_0 $mark w0
			pc_pipe := pc_0 $mark ww0
			$guard (d_1) ir_pipe := ir_1 $mark w1 
			$guard (d_1) pc_pipe := pc_1 $mark ww1 
			$guard (d_2) ir_pipe := ir_2 $mark w2 
			$guard (d_2) pc_pipe := pc_2 $mark ww2
			$guard (d_3) ir_pipe := ir_3 
			$guard (d_3) pc_pipe := pc_3

			$guard (~ branch_case) next_pc_pipe := (pc_0 + 4)

		$while 1	
		
	}
	
}

$module [registerFile] $in () $out () $is
{
	$storage reg:$array [16] $of $uint<16>
		
	$branchblock [init] {
                $merge $entry loopback
			$phi I := ($bitcast ($uint<4>) 0) $on $entry NI  $on loopback
		$endmerge
		reg[I] := 0 
		NI := (I+1)
		$if (I < 15) $then $place [loopback] $endif
	}

	$parallelblock [Ports] {

		$branchblock[read_port_0]
		{
    			$dopipeline $depth 8 $buffering 2
				$merge $entry $loopback $endmerge
				block_read_id := block_read_request_0 // always read a value
				rid := reg_file_read_port_0_request   // wait until rid is non-zero
				rpc := reg_file_read_port_0_request_pc // pc for instruction requesting read.
				
				real_read := (rid != 255)

				write_pending := (block_read_id != 255) // is write-pending?
				wpc := bypass_pc_to_0

				wmatch := ((write_pending & (rid == block_read_id)) & (wpc != rpc)) 
				bypass  := (real_read & wmatch)
				nobypass := (real_read & (~ wmatch))

				$guard (nobypass) reg_file_read_port_0_response := reg[rid] // real-read.
				$guard (write_pending) written_val := bypass_to_0  // if write-pending get bypassed value
				$guard (bypass) reg_file_read_port_0_response := written_val // bypassed-read.
			$while 1
		}
		$branchblock[read_port_1]
		{
    			$dopipeline $depth 8 $buffering 2
				$merge $entry $loopback $endmerge
				block_read_id := block_read_request_1 // always read a value
				rid := reg_file_read_port_1_request   // wait until rid is non-zero
				rpc := reg_file_read_port_1_request_pc // pc for instruction requesting read.

				real_read := (rid != 255)

				write_pending := (block_read_id != 255) // is write-pending?
				wpc := bypass_pc_to_1

				wmatch := ((write_pending & (rid == block_read_id)) & (wpc != rpc)) 
				bypass  := (real_read & wmatch)
				nobypass := (real_read & (~ wmatch))

				$guard (nobypass) reg_file_read_port_1_response := reg[rid] // real-read.
				$guard (write_pending) written_val := bypass_to_1  // if write-pending get bypassed value
				$guard (bypass) reg_file_read_port_1_response := written_val // bypassed-read.

			$while 1
		}
		$branchblock[read_port_2]
		{
    			$dopipeline $depth 8 $buffering 2
				$merge $entry $loopback $endmerge
				block_read_id := block_read_request_2 // always read a value
				rid := reg_file_read_port_2_request   // wait until rid is non-zero
				rpc := reg_file_read_port_2_request_pc // pc for instruction requesting read.

				real_read := (rid != 255)

				write_pending := (block_read_id != 255) // is write-pending?
				wpc := bypass_pc_to_2

				wmatch := ((write_pending & (rid == block_read_id)) & (wpc != rpc)) 
				bypass  := (real_read & wmatch)
				nobypass := (real_read & (~ wmatch))

				$guard (nobypass) reg_file_read_port_2_response := reg[rid] // real-read.
				$guard (write_pending) written_val := bypass_to_2  // if write-pending get bypassed value
				$guard (bypass) reg_file_read_port_2_response := written_val // bypassed-read.
			$while 1
		}
		$branchblock[write_port]
		{
			$dopipeline $depth 8 $buffering 2
				$merge $entry $loopback $endmerge
				write_id := reg_file_write_port_request
				wpc  := reg_file_write_port_request_pc  

				real_write := (write_id != 255)

				bypass_pc_to_0 :=  wpc
				bypass_pc_to_1 :=  wpc
				bypass_pc_to_2 :=  wpc

				$guard (real_write) write_data := reg_file_write_port_data
				$guard (real_write) reg[write_id] := write_data

				// NOTE: timing could be an issue here?
				//       the bypass takes place in parallel with the
				//       write, but that is ok.
				$guard (real_write) bypass_to_0 :=  write_data
				$guard (real_write) bypass_to_1 :=  write_data
				$guard (real_write) bypass_to_2 :=  write_data
			$while 1
		}
	}		
}


$module [execute] 
$in ()
$out ()
$is 
{
  	$branchblock [core] 
  	{
    		$dopipeline $depth 8 $buffering 2
		
    		$merge $entry $loopback $endmerge

		// stage 1
		ir    := ir_pipe
		pc    := pc_pipe

		enpc  := (pc + 1)
	
		// stage 3
  		op := (ir >> 12) // 2
  		r1 := ((ir >> 8) & _h000f) // 2  // get the first register */
  		r2 := ((ir >> 4) & _h000f) // 2  // get the second register */
  		r3 := (ir & _h000f)        // 2  // get the third register */
  		imm := (ir & _h00ff)       // 2 // get the immediate value */
	
		// stage 4
  		offs := (($mux (imm > 127) (128 - imm)  imm) - 1)  //3 // get the offset, and subtract 1 because
                                                  	// the PC has already moved to next location */
	
		// stage 5
		op0 := (op == 0) op1 := (op == 1) op2 := (op == 2) op3 := (op == 3)  
        	op4 := (op == 4) op5 := (op == 5) op6 := (op == 6) op7 := (op ==7)	
        	op8 := (op == 8) op9 := (op == 9) op10 := (op == 10) op14 := (op == 14)	 // 3
	
        	branch_case := (op8 | (op9 | op10)) 

		read_r2 := (((op1 | op2) | (op3 | op4)) | op5)
		read_r3 := ((op1 | op2) | (op3 | op4))
		read_r1 := (((op5 | op6) | (op9 | op10)) | (op4 | op14))

		write_r1 := (((op1 | op2) | (op3 | op5)) | (op6 | op7))
	
		block_read_request_0 := ($mux write_r1 r1 255)
		block_read_request_1 := ($mux write_r1 r1 255)
		block_read_request_2 := ($mux write_r1 r1 255)
		
		// stage 7: register read.
		reg_file_read_port_0_request := ($mux read_r1 r1 255)
		reg_file_read_port_0_request_pc := pc
		$guard (read_r1) r1_val := reg_file_read_port_0_response

		reg_file_read_port_1_request := ($mux read_r2 r2 255)
		reg_file_read_port_1_request_pc := pc
		$guard (read_r2) r2_val := reg_file_read_port_1_response

		reg_file_read_port_2_request := ($mux read_r3 r3 255)
		reg_file_read_port_2_request_pc := pc
		$guard (read_r3) r3_val := reg_file_read_port_2_response

		// write request.
		reg_file_write_port_request_pc := pc
		reg_file_write_port_request := ($mux write_r1 r1 255)
	
		// stage 8: write_results..
		// note: memory load/store from DCACHE.
		$guard (op1)  reg_file_write_port_data := (r2_val + r3_val)
		$guard (op2)  reg_file_write_port_data := (r2_val - r3_val)
		$guard (op3)  op3_addr  := (r2_val + r3_val)
        	$guard (op3)  reg_file_write_port_data := mem[op3_addr]  // 7
		$guard (op4)  op4_addr := (r2_val + r3_val)
        	$guard (op4)  mem[op4_addr] := r1_val // 7
		$guard (op5)  reg_file_write_port_data := ((r1_val & _hff00)  | imm) // 6
		$guard (op6)  reg_file_write_port_data := ((r1_val & _h00ff) | (imm << 8)) // 6
		$guard (op7)  reg_file_write_port_data := r2_val // 6
		$guard (op14) out_port := r1_val
	
		$guard (op8)  next_pc_pipe := mem[enpc] // 4
 		$guard (op9)  next_pc_pipe := (enpc + (r1_val + offs)) // 6
		$guard (op10) next_pc_pipe := ( $mux (r1_val == 0) (enpc + offs) enpc) // 6

		// next-pc not updated, halt flagged.
		$guard (op0) halt_pipe := 1

    		$while 1
  	}
}
	
